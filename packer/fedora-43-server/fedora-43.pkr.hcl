packer {
  required_plugins {
    proxmox = {
      version = ">= 1.1.3"
      source  = "github.com/hashicorp/proxmox"
    }
  }
}

locals {
  vm_name              = "fedora-43-base"
  template_desc_prefix = "Fedora 43 Server Base Image for"
  template_desc_suffix = "- Built on ${timestamp()}"
  cores                = 2
  memory               = 2048
  scsi_controller      = "virtio-scsi-pci"
  disk_size            = "20G"
  disk_format          = "raw"
  disk_storage_pool    = "ssd-zfs"
  disk_type            = "scsi"
  disk_discard         = true
  disk_ssd             = true
  network_model        = "virtio"
  network_bridge       = "vmbr0"
  network_firewall     = false
  cloud_init_storage   = "ssd-zfs"
  boot_wait            = "10s"
  boot_command = [
    "<wait>e<wait>",
    "<down><down><end><wait>",
    " inst.text inst.ks=http://{{ .HTTPIP }}:{{ .HTTPPort }}/ks.cfg",
    "<leftCtrlOn>x<leftCtrlOff>"
  ]
  ssh_timeout            = "20m"
  ssh_handshake_attempts = 100
  http_directory         = "${path.root}/${var.http_directory}"

  # VM ID calculation: Fedora base (9020) + node number
  distro_base_id = 9020
  pve2_vm_id     = 9022 # 9020 + 2
  # To add pve1: pve1_vm_id = 9021
  # To add pve3: pve3_vm_id = 9023
}

# ------------------------------------------------------------------------------
# Source: Template on pve2
# ------------------------------------------------------------------------------
source "proxmox-iso" "fedora-base-pve2" {
  # --- Connection Details ---
  proxmox_url              = var.proxmox_api_url
  username                 = var.proxmox_api_token_id
  token                    = var.proxmox_api_token_secret
  insecure_skip_tls_verify = true
  node                     = "pve2"
  task_timeout             = "20m"

  # --- VM Configuration ---
  vm_id                = local.pve2_vm_id
  vm_name              = local.vm_name
  template_description = "${local.template_desc_prefix} pve2 ${local.template_desc_suffix}"
  tags                 = "packer;pve2"

  # --- Common Hardware & Build Config ---
  cores           = local.cores
  memory          = local.memory
  scsi_controller = local.scsi_controller
  disks {
    disk_size    = local.disk_size
    format       = local.disk_format
    storage_pool = local.disk_storage_pool
    type         = local.disk_type
    discard      = local.disk_discard
    ssd          = local.disk_ssd
  }
  network_adapters {
    model    = local.network_model
    bridge   = local.network_bridge
    firewall = local.network_firewall
  }
  cloud_init              = true
  cloud_init_storage_pool = local.cloud_init_storage
  boot_iso {
    type     = "scsi"
    iso_file = "${var.iso_storage_pool}:iso/${var.iso_name}"
    unmount  = true
  }
  boot_wait              = local.boot_wait
  boot_command           = local.boot_command
  ssh_username           = "fedora"
  ssh_private_key_file   = var.ssh_private_key_file
  ssh_timeout            = local.ssh_timeout
  ssh_handshake_attempts = local.ssh_handshake_attempts
  http_directory         = local.http_directory
  qemu_agent             = true
}

# ------------------------------------------------------------------------------
# Build Block: Defines provisioners
# ------------------------------------------------------------------------------
build {
  sources = [
    "source.proxmox-iso.fedora-base-pve2"
  ]

  # NOTE: ks.cfg is generated by build.sh before packer runs
  # Run: ./build.sh instead of packer build .

  provisioner "file" {
    source      = "${path.root}/scripts/setup.sh"
    destination = "/tmp/setup.sh"
  }

  provisioner "shell" {
    execute_command = "sudo -E sh -c '{{ .Vars }} {{ .Path }}'"
    inline = [
      "chmod +x /tmp/setup.sh",
      "/tmp/setup.sh"
    ]
  }

  provisioner "shell" {
    execute_command = "sudo -E sh -c '{{ .Vars }} {{ .Path }}'"
    inline = [
      "rm -rf /root/.ssh || true"
    ]
  }
}
